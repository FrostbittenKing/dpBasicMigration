' address: 0x88c1 init:  name: Geldw.
00010 REM Bandreservierung
00020 DIM AB(24)
00030 REM *************
00040 REM Hauptprogramm
00050 REM *************
00060 INIT 6:STORE
00070 PRINT "*******************"
00080 PRINT"*                 *"
00090 PRINT"*Cassettenrogramm*"
00100 PRINT"*                 *"
00110 PRINT"*******************"
00120 PRINT:PRINT
00130 PRINT "1=Dateneingabe"
00140 PRINT
00150 PRINT "2=Lesen der Daten ";
00160 PRINT "von der Cassette":PRINT
00170 PRINT "3=Graphische Darstellung"
00180 PRINT
00190 PRINT "4=Ueberspielen auf ";
00200 PRINT "Cassette":PRINT
00210 PRINT "5=Programmende"
00220 PRINT:PRINT:SCREEN:DISPLAY
00230 INPUT "Sie koennen waehlen ";K
00240 IF K=5 THEN END
00250 IF (K<1)OR(K>4) GOTO 280
00260 ON K GOSUB 1000,2000,3000,4000
00270 GOTO 60
00280 PRINT "Waehlen Sie bitte richtig"
00290 GOTO 230
01000 REM ************
01010 REM Dateneingabe
01020 REM ************
01030 INIT 6:STORE
01040 A=0:B=0
01050 FOR I=1 TO 12:A=A+AB(I):NEXT I
01060 FOR I=13 TO 24:B=B+AB(I):NEXT I
01070 PRINT TAB(5);"Einkommen %";
01080 PRINT TAB(22);"Ausgaben %"
01110 RESTORE
01120 FOR I=1 TO 12
01130 READ C$:PRINT C$;
01140 PRINT TAB(5);AB(I);TAB(15);
01150 GOSUB 5000
01160 I=I+12
01170 PRINT TAB(22);AB(I);TAB(31);
01180 GOSUB 6000
01190 I=I-12
01200 NEXT I
01230 PRINT "TOT";TAB(5);A;TAB(15);
01240 PRINT "100%";TAB(22);B;TAB(31);
01250 PRINT "100%":SCREEN:DISPLAY
01260 INPUT "Monatscode (0=ENDE) ";M
01270 MC=INT(M)
01280 IF MC=0 THEN RETURN
01290 IF (MC<1)OR(MC>12) GOTO 1260
01300 INPUT "Einkommen in DM ";IK
01310 INPUT "Ausgaben in DM ";UI
01320 INPUT "Stimmt (J/N) ";OK$
01330 IF OK$="J" GOTO 1360
01340 IF OK$="j" GOTO 1360
01350 GOTO 1260
01360 AB(MC)=IK:MC=MC+12:AB(MC)=UI
01370 GOTO 1030
01500 DATA JAN,FEB,MAR,APR,MAI,JUN
01510 DATA JUL,AUG,SEP,OKT,NOV,DEZ
02000 REM **********************
02010 REM Lesen von der Cassette
02020 REM **********************
02030 INIT 6
02040 PRINT"Datencassette einlegen"
02050 PRINT"und zum Anfang"
02060 PRINT"zurueckspulen"
02080 PRINT
02090 PRINT"Starttaste am"
02100 PRINT"Cassettenrecorder druecken"
02110 PRINT"und RET-Taste am G7400 ";
02115 PRINT"druecken ";
02120 REM INPUT T$:POKE -30762,3
02130 REM CLOAD* "NUDATA" AB
02140 PRINT
02150 PRINT "Einlesen beendet"
02160 FOR I=1 TO 3000:NEXT I
02170 RETURN
03000 REM **********************
03010 REM Graphische Darstellung
03020 REM **********************
03025 PRINT CHR$(150);
03030 INIT 3:PRINT CHR$(150);
03035 PRINT " Einen Moment bitte";
03040 STORE:PRINT:PRINT:T=4250
03050 FOR I=1 TO 16
03060 T=T-250
03070 IF T=500 THEN PRINT TAB(1);
03080 GOSUB 9000
03090 GOSUB 8000
03100 NEXT I
03110 PRINT TAB(6);
03120 FOR I=1 TO 26
03130 PRINT CHR$(126);
03140 NEXT I:PRINT
03150 PRINT TAB(7);
03160 PRINT "J F M A M J J A S O N D"
03170 PRINT:PRINT CHR$(156);
03180 TX 4,0,0:PRINT "^";
03190 TX 0,0,0:PRINT "=Einkommen ";
03200 TX 1,0,0:PRINT "^";
03210 TX 0,0,0:PRINT "=Ausgaben"
03220 SCREEN:DISPLAY
03230 PRINT"Bitte RET-Taste druecken ";
03235 INPUT OK$
03240 RETURN
04000 REM *************************
04010 REM Auf Cassette ueberspielen
04020 REM *************************
04030 INIT 6
04040 PRINT "Leercassette einlegen"
04050 PRINT "und ";
04060 PRINT "zum Anfang"
04070 PRINT "zurueckspulen"
04080 PRINT
04090 PRINT "Aufnahme- und Wiedergabe-"
04100 PRINT "Taste am Recorder"
04110 PRINT "und die RET-Taste"
04120 PRINT "am G7400 druecken ";
04130 INPUT T$
04150 REM CSAVE* "NUDATA" AB
04160 PRINT
04170 PRINT "Ueberspielung beendet"
04180 PRINT "Die Cassette ist nun "
04190 PRINT "eine Datencassette"
04200 FOR I=1 TO 3000:NEXT I
04210 RETURN
05000 REM ***************************
05010 REM % Berechnung des Einkommens
05020 REM ***************************
05030 IF A=0 THEN Y=0:GOTO 5050
05040 Z=AB(I)*100/A:Y=INT(Z)
05050 PRINT Y;"%";
05060 RETURN
06000 REM *************************
06010 REM % Berechnung der Ausgaben
06020 REM *************************
06030 IF B=0 THEN Y=0:GOTO 6050
06040 Z=AB(I)*100/B:Y=INT(Z)
06050 PRINT Y;"%"
06060 RETURN
08000 REM *******
08010 REM Spalten
08020 REM *******
08030 PRINT TAB(6);CHR$(123);
08040 FOR J=1 TO 12
08050 TX 4,0,0
08060 GOSUB 8500
08070 J=J+12
08080 TX 1,0,0
08090 GOSUB 8500
08100 J=J-12:NEXT J
08110 TX 0,0,0:PRINT
08120 RETURN
08500 IF T<>250 GOTO 8520
08510 IF AB(J)>0 GOTO 850
08520 IF AB(J)>T GOTO 8540
08530 PRINT " ";:GOTO 8550
08540 PRINT "^";
08550 RETURN
09000 REM *********************
09010 REM Vertikale Limitierung
09020 REM *********************
09030 IF T= 500 THEN PRINT T;
09040 IF T=1000 THEN PRINT T;
09050 IF T=1500 THEN PRINT T;
09060 IF T=2000 THEN PRINT T;
09070 IF T=2500 THEN PRINT T;
09080 IF T=3000 THEN PRINT T;
09090 IF T=3500 THEN PRINT T;
09100 IF T=4000 THEN PRINT T;
09110 RETURN
